// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task startLocalServer() {
    group 'customize'
    description 'Launch the local server.'

    project(":backend").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }

    dependsOn ':backend:appengineRun'
    doLast {
        'Server started.'
    }
}

task runConnectedTest() {
    group 'customize'
    description 'Run the test cases.'

    dependsOn ':app:connectedCheck'
    doLast {
        'Finished all test cases. Please check reports.'
    }
    shouldRunAfter 'startLocalServer'
}

task stopLocalServer() {
    group 'customize'
    description 'Terminate the local server.'

    dependsOn ':backend:appengineStop'
    doLast {
        println 'Server terminated.'
    }
    shouldRunAfter 'runConnectedTest'
}

task autoConnectedCheck () {
    group 'customize'
    description 'Run the test cases with launching and terminating the local server.'

    dependsOn = ['startLocalServer', 'runConnectedTest', 'stopLocalServer']
}